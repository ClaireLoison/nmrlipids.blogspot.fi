set term pdfcairo enhanced rounded color dashed dashlength 1 size 16.2cm, 10cm
set out OUTPUTFILE

w=10   # linewidth simulation
r=2 # point radius simulation

set style line 11 lt 2 pt 7 ps r lw w lc rgb "#D70039" # for Berger
set style line 12 lt 1 pt 7 ps r lw w lc rgb "#0088BF" # for CHARMM36
set style line 13 lt 2 pt 7 ps r lw w lc rgb "#FFD300" # for GAFF
set style line 14 lt 1 pt 7 ps r lw w lc rgb "#00A368" # for MacRog
set style line 14 lt 1 pt 11 ps r lw w lc rgb "#00A368" # for MacRog

set key l at screen 0.12,0.61 Left reverse samplen 5 spacing 2 width 2 maxrows 5 font ",14"
set xlabel "n_{w/l}" font ",14"
set ylabel "deviation of P-N vector from membrane normal (degrees)"

plot [60:0][67:83] \
     DATA_BERGER u ($1):($2) w lp ls 11 \
     t " POPC 298K Berger", \
     DATA_BERGER_DLPC u ($1):($2) w lp ls 11 \
     t " DLPC 300K Berger", \
     DATA_MACROG u ($1):($2) w lp ls 14 \
     t " &{PO}@{\"}&{PC} 310K MacRog", \
     DATA_CHARMM36 u ($1):($2) w lp ls 12 \
     t " &{PO}@{\"}&{PC} 303K CHARMM36", \
     DATA_GAFF u ($1):($2) w lp ls 13 \
     t " &{PO}@{\"}&{PC} &{30}@{\"}&{3K} GAFF"

					