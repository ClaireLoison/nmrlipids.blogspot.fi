set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 21cm #16.2cm
set out "cholesterolization.pdf"

set multiplot layout 7,1 tit " "
set tmargin 0
set bmargin 0
set rmargin 1
set lmargin 11
set xtics format " "
set mxtics 2
set ytics 0.02
set mytics 2
set ylabel "S_{CH}" offset screen -0.02, graph 0.43 norotate font ",18"
set label 141 at screen 0.09, graph 0.77 right font ",9" tc rgb "#00A368" # MacRog
set label 111 at screen 0.09, graph 0.67 right font ",9" tc rgb "#D70039" # Berger
set label 121 at screen 0.09, graph 0.57 right font ",9" tc rgb "#0088BF" # CHARMM36
#set label 131 at screen 0.09, graph 0.47 right font ",9" tc rgb "#FFD300" # GAFF

mPOPC=760.076
mH2O=18.01528 

we=1   # linewidth experimental
re=0.4 # point radius experimental

w=2   # linewidth simulation
r=0.5 # point radius simulation

set style line  1 lt 3 pt 4 ps re lw we lc rgb "#000000" # for Brown (EXP)
set style line  2 lt 3 pt 6 ps re lw we lc rgb "#000000" # for Ferreira (EXP)
set style line  3 lt 3 pt 8 ps re lw we lc rgb "#000000" # for  (EXP)

set style line 11 lt 1 pt 7 ps r lw w lc rgb "#D70039" # for Berger
set style line 12 lt 1 pt 7 ps r lw w lc rgb "#0088BF" # for CHARMM36
set style line 13 lt 2 pt 7 ps r lw w lc rgb "#FFD300" # for GAFF
set style line 14 lt 1 pt 7 ps r lw w lc rgb "#00A368" # for MacRog
set style line 15 lt 1 pt 6 ps r lw w lc rgb "#0088BF" # for CHARMM36

set ytics -2,0.02,2

# Carbon: beta ####################################
set label 100 "{/Symbol b}" at graph 0.05,0.15 font ",18"
set key l at screen 0.02,0.07 Left reverse samplen 2 width 2 maxrows 4 font ",14"
# offsets:
d_MR=-0.05 # MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=-0.07 # Berger
set label 111 sprintf("S_{CH} %+g",d_Be) # Berger
d_CH=0.03 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=-0.04 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][-0.07:0.01]\
    "./DPPC/cholesterol/EXP-Brown1978.dat" u ($1):($2) every :::0::0 w lp ls 1 \
    t "DPPC 331K ^{_{^{2}}}H-NMR", \
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::1::1 w lp ls 2 \
    t "@{POPC}&{DPPC} 300K ^{_{^{13}}}C-NMR", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::0::1 w lp ls 14 \
    t " {&{PO}}{@{\"}}&{PC} 310K MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::0::1 w lp ls 11 \
    t " POPC 298K Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::0::1 w lp ls 12 \
    t " &{PO}@{\"}&{PC} 303K CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::0::1 w lp ls 15 \
    t " &{PO}@{\"}&{PC} 298K CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::0::1 w lp ls 13 \
#    t " {&{PO}}{@{\"}}&{PC} &{30}@{\"}&{3K} GAFF"
unset key

# Carbon: alpha ###################################
set label 100 "{/Symbol a}"
# offsets:
d_MR=+0.01 #MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=-0.04 # Berger
set label 111 sprintf("S_{CH} %+g",d_Be) # Berger
d_CH=0.01 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=-0.01 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][0.01:0.11] \
    "./DPPC/cholesterol/EXP-Brown1978.dat" u ($1):($2) every :::1::2 w lp ls 1 t "Brown", \
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::2::2 w lp ls 2 t "Ferreira", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::2::3 w lp ls 14 t "MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::2::3 w lp ls 11 t "Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::2::3 w lp ls 12 t "CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::2::3 w lp ls 15 t "CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::2::3 w lp ls 13 t "GAFF"

# Carbon: g3 ######################################
# R and S together:
set label 100 "g_{3}" at graph 0.05,0.85
# offsets:
d_MR=+0.0 #MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=+0.03 # Berger
set label 111 sprintf("S_{CH} %+3.2f",d_Be) # Berger
d_CH=0.02 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=0.03 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][-0.25:-0.07]\
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::3::3 w lp ls 2 t "Ferreira", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::4::5 w lp ls 14 t "MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::4::5 w lp ls 11 t "Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::4::5 w lp ls 12 t "CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::4::5 w lp ls 15 t "CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::4::5 w lp ls 13 t "GAFF"


# Carbon: g2 ######################################
set label 100 "g_{2}"
# offsets:
d_MR=0.0 #MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=-0.02 # Berger
set label 111 sprintf("S_{CH} %+3.2f",d_Be) # Berger
d_CH=0.0 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=0.06 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][-0.25:-0.15] \
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::4::4 w lp ls 2 t "Ferreira", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::6::6 w lp ls 14 t "MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::6::6 w lp ls 11 t "Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::6::6 w lp ls 12 t "CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::6::6 w lp ls 15 t "CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::6::6 w lp ls 13 t "GAFF"

# Carbon: g1 ######################################
# R:
set label 100 "g_{1R}" #at graph 0.05,0.15
# offsets:
d_MR=0.01 #MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=-0.32 # Berger
set label 111 sprintf("S_{CH} %+3.2f",d_Be) # Berger
d_CH=0.03 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=0.03 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][-0.21:-0.09] \
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::5::5 w lp ls 2 t "Ferreira", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::7::7 w lp ls 14 t "MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::7::7 w lp ls 11 t "Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::7::7 w lp ls 12 t "CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::7::7 w lp ls 15 t "CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::7::7 w lp ls 13 t "GAFF"
# S:
set label 100 "g_{1S}"
set xtics format "%g"
set xlabel "cholesterol content (mol%)"
# offsets:
d_MR=-0.02 #MacRog
set label 141 sprintf("S_{CH} %+g",d_MR) # MacRog
d_Be=-0.08 # Berger
set label 111 sprintf("S_{CH} %+3.2f",d_Be) # Berger
d_CH=0.04 # CHARMM36
set label 121 sprintf("S_{CH} %+g",d_CH) # CHARMM36
#d_GA=-0.08 # GAFF
#set label 131 sprintf("S_{CH} %+g",d_GA) # GAFF
plot [-5:65][-0.09:0.05] \
    "./POPC/cholesterol/EXP-Ferreira2013.dat" u ($1):($2) every :::6::6 w lp ls 2 t "Ferreira", \
    "./POPC/cholesterol/MACROG-310K_blogged-18-09-14.dat" u ($1):($2+d_MR) ev :::8::8 w lp ls 14 t "MacRog", \
    "./POPC/cholesterol/BERGER-298K.dat" u ($1):($2+d_Be) ev :::8::8 w lp ls 11 t "Berger", \
    "./POPC/cholesterol/CHARMM36-303K_blogged-01-12-14.dat" u ($1):($2+d_CH) ev :::8::8 w lp ls 12 t "CHARMM36", \
    "./POPC/cholesterol/CHARMM36-298K_blogged-24-2-15.dat" u ($1):($2+d_CH) ev :::8::8 w lp ls 15 t "CHARMM36"
#    "./POPC/cholesterol/GAFF-303K.dat" u ($1):($2+d_GA) ev :::8::8 w lp ls 13 t "GAFF"
unset multiplot